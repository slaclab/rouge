# ----------------------------------------------------------------------------
# Title      : Rogue Cmake Export Template
# ----------------------------------------------------------------------------
# File       : RogueConfig.cmake.in
# ----------------------------------------------------------------------------
# Description:
# Rogue export file to be overridden by Cmake.
# ----------------------------------------------------------------------------
# This file is part of the rogue software package. It is subject to
# the license terms in the LICENSE.txt file found in the top-level directory
# of this distribution and at:
#    https://confluence.slac.stanford.edu/display/ppareg/LICENSE.html.
# No part of the rogue software package, including this file, may be
# copied, modified, propagated, or distributed except according to the terms
# contained in the LICENSE.txt file.
# ----------------------------------------------------------------------------

# Rogue cmake configuration file. Used when compiling against the rogue library.

#####################################
# Enables from Rogue CMAKE
#####################################
set(NO_PYTHON   @NO_PYTHON@)
set(NO_EPICS    @NO_EPICS@)

#####################################
# Boost + Python
#####################################
if ( NOT NO_PYTHON )
   find_package(PythonInterp 3 QUIET REQUIRED)
   find_package(PythonLibs 3 QUIET REQUIRED)

   # Find Numpy
   execute_process(
      COMMAND python3 -c "import numpy; print(numpy.get_include(), end='')"
      RESULT_VARIABLE Python3_NumPy_RESULT
      OUTPUT_VARIABLE Python3_NumPy_INCLUDE_DIRS
      ERROR_QUIET
      OUTPUT_STRIP_TRAILING_WHITESPACE)

   if (Python3_NumPy_RESULT)
      message("")
      message(FATAL_ERROR "Failed to find numpy includes!")
   endif()

   set(Boost_USE_MULTITHREADED ON)
   set(Boost_NO_BOOST_CMAKE ON)

   # Hint for boost on anaconda
   if (DEFINED ENV{CONDA_PREFIX})
      set(BOOST_ROOT $ENV{CONDA_PREFIX})

   # SLAC AFS custom path
   elseif (DEFINED ENV{BOOST_PATH})
      set(BOOST_ROOT $ENV{BOOST_PATH})
   endif()

   # libboost_python3.7 style libraries
   message("Looking for libboost_python${PYTHON_VERSION_MAJOR}${PYTHON_VERSION_MINOR}")
   FIND_PACKAGE(Boost 1.58 QUIET COMPONENTS python${PYTHON_VERSION_MAJOR}${PYTHON_VERSION_MINOR})

   # libboost_python3 style libraries
   if (NOT Boost_FOUND)
      message("Looking for libboost_python${PYTHON_VERSION_MAJOR}")
      FIND_PACKAGE(Boost 1.58 QUIET COMPONENTS python${PYTHON_VERSION_MAJOR})
   endif()

   # libboost_python style libraries
   if (NOT Boost_FOUND)
      message("Looking for libboost_python")
      FIND_PACKAGE(Boost 1.58 QUIET COMPONENTS system thread python)
   endif()

   # libboost_python-py37 style libraries
   if (NOT Boost_FOUND)
      message("Looking for libboost_python-py${PYTHON_VERSION_MAJOR}${PYTHON_VERSION_MINOR}")
      FIND_PACKAGE(Boost 1.58 QUIET COMPONENTS python-py${PYTHON_VERSION_MAJOR}${PYTHON_VERSION_MINOR})
   endif()

   # libboost_python3-py37 style libraries
   if (NOT Boost_FOUND)
      message("Looking for libboost_python3-py${PYTHON_VERSION_MAJOR}${PYTHON_VERSION_MINOR}")
      FIND_PACKAGE(Boost 1.58 QUIET COMPONENTS python3-py${PYTHON_VERSION_MAJOR}${PYTHON_VERSION_MINOR})
   endif()

   # Nothing worked
   if (NOT Boost_FOUND)
      message("")
      message(FATAL_ERROR "Failed to find boost libraries!")
   endif()
else()
   add_definitions( -DNO_PYTHON )
endif()

#####################################
# BZIP2
#####################################
find_package(BZip2 QUIET REQUIRED)

#####################################
# ZeroMQ
#####################################
# First try with cmake packages
find_package(ZeroMQ QUIET)

# ZeroMQ does not always support cmake, use brute force
if (NOT ZeroMQ_FOUND)

   # Convert LD_LIBRARY PATH for search
   if(DEFINED ENV{LD_LIBRARY_PATH})
      string(REPLACE ":" ";" HINT_PATHS $ENV{LD_LIBRARY_PATH})
   else()
      set(HINT_PATHS, "")
   endif()

   # See if zmq library is in LD_LIBRARY_PATH
   find_library(ZeroMQ_LIBRARY
                NAMES zmq
                PATHS ${HINT_PATHS})

   # Found it
   if (ZeroMQ_LIBRARY)

      # Get the base directory
      get_filename_component(ZMQ_LIBDIR ${ZeroMQ_LIBRARY} DIRECTORY)

      # See if we can find the header in a relative path
      find_path(ZeroMQ_INCLUDE_DIR
              NAMES zmq.h
              PATHS ${ZMQ_LIBDIR}/../include
              )
      if (NOT ZeroMQ_INCLUDE_DIR)
         message("")
         message(FATAL_ERROR "Failed to find ZeroMQ_INCLUDE_DIR!")
      endif()
   else()
      # Failed to find it
      message("")
      message(FATAL_ERROR "Failed to find zeroMQ!")
   endif()
endif()

#####################################
# EPICS V3
#####################################
if((NOT NO_PYTHON) AND (NOT NO_EPICS) AND DEFINED ENV{EPICS_BASE})
   set(DO_EPICS_V3 1)
   set(EPICS_BASE_DIR $ENV{EPICS_BASE})
   if(DEFINED ENV{EPICS_HOST_ARCH})
       set(EPICS_ARCH $ENV{EPICS_HOST_ARCH})
   else()
       execute_process(COMMAND ${EPICS_BASE_DIR}/startup/EpicsHostArch
                       OUTPUT_VARIABLE EPICS_ARCH OUTPUT_STRIP_TRAILING_WHITESPACE)
       string(REGEX REPLACE "\n$" "" EPICS_ARCH "${EPICS_ARCH}")
   endif()

   # PCAS is part of epics before version 4
   if(EPICS_PCAS_ROOT)
      set(EPICS_PCAS_DIR ${EPICS_PCAS_ROOT})
   elseif(DEFINED ENV{EPICS_PCAS_ROOT})
      set(EPICS_PCAS_DIR $ENV{EPICS_PCAS_ROOT})
   else()
      set(EPICS_PCAS_DIR $ENV{EPICS_BASE})
   endif()

   # See if we can find the pcas header, may not exist if epics 7 without pcas is installed
   find_path(E3_TEST_INCLUDE_DIR NAMES casdef.h PATHS ${EPICS_PCAS_DIR}/include)
   if (NOT E3_TEST_INCLUDE_DIR)
      set(DO_EPICS_V3 0)
   elseif(APPLE)

      set(EPICS_INCLUDES    ${EPICS_PCAS_DIR}/include
                            ${EPICS_BASE_DIR}/include
                            ${EPICS_BASE_DIR}/include/compiler/gcc
                            ${EPICS_BASE_DIR}/include/os/Darwin)

      set(EPICS_LIBRARIES   ${EPICS_PCAS_DIR}/lib/${EPICS_ARCH}/libcas.dylib
                            ${EPICS_PCAS_DIR}/lib/${EPICS_ARCH}/libgdd.dylib
                            ${EPICS_BASE_DIR}/lib/${EPICS_ARCH}/libca.dylib
                            ${EPICS_BASE_DIR}/lib/${EPICS_ARCH}/libCom.dylib )
   else()

      set(EPICS_INCLUDES    ${EPICS_PCAS_DIR}/include
                            ${EPICS_BASE_DIR}/include
                            ${EPICS_BASE_DIR}/include/compiler/gcc
                            ${EPICS_BASE_DIR}/include/os/Linux)

      set(EPICS_LIBRARIES   ${EPICS_PCAS_DIR}/lib/${EPICS_ARCH}/libcas.so
                            ${EPICS_PCAS_DIR}/lib/${EPICS_ARCH}/libgdd.so
                            ${EPICS_BASE_DIR}/lib/${EPICS_ARCH}/libca.so
                            ${EPICS_BASE_DIR}/lib/${EPICS_ARCH}/libCom.so )
   endif()

else()
   set(DO_EPICS_V3 0)
endif()

#####################################
# Set Rogue Variables
#####################################
# These variables are now defined
# ${Boost_INCLUDE_DIRS}
# ${Boost_LIBRARIES}
# ${PYTHON_INCLUDE_DIRS}
# ${PYTHON_LIBRARIES}
# ${ZeroMQ_INCLUDE_DIR}
# ${ZeroMQ_LIBRARY}
# ${BZIP2_INCLUDE_DIR}
# ${BZIP2_LIBRARIES}
# ${EPICS_INCLUDES})
# ${EPICS_LIBRARIES})

# Rogue Include paths
set(ROGUE_INCLUDE_ONLY @CONF_INCLUDE_DIRS@)

# Rogue libraries
set(ROGUE_LIBRARIES_ONLY @CONF_LIBRARIES@)

# Rogue Include paths
set(ROGUE_INCLUDE_DIRS @CONF_INCLUDE_DIRS@
                       ${Boost_INCLUDE_DIRS}
                       ${PYTHON_INCLUDE_DIRS}
                       ${Python3_NumPy_INCLUDE_DIRS}
                       ${ZeroMQ_INCLUDE_DIR}
                       ${BZIP2_INCLUDE_DIR}
                       ${EPICS_INCLUDES})

# Rogue libraries
set(ROGUE_LIBRARIES @CONF_LIBRARIES@
                    ${Boost_LIBRARIES}
                    ${PYTHON_LIBRARIES}
                    ${ZeroMQ_LIBRARY}
                    ${BZIP2_LIBRARIES}
                    ${EPICS_LIBRARIES})

# Set Version
set(ROGUE_VERSION @ROGUE_VERSION@)

# Set Install Directory
set(ROGUE_DIR @ROGUE_DIR@)

#####################################
# Print status
#####################################

message("")
message("----------------------------------------------------------------------")
message("-- Rogue Version: ${ROGUE_VERSION}")
message("")
message("-- Rogue Directory: ${ROGUE_DIR}")
message("")

if (NO_PYTHON)
   message("-- Compiling without boost & python!")
else()
   message("-- Found boost: ${Boost_INCLUDE_DIRS}")
   message("-- Found boost: ${Boost_LIBRARIES}")
   message("")
   message("-- Found python: ${PYTHON_LIBRARIES}")
   message("")
   message("-- Found numpy: ${Python3_NumPy_INCLUDE_DIRS}")
endif()

message("")

if (DO_EPICS_V3)
   message("-- Found EPICS: ${EPICS_BASE_DIR}")
   message("")
   message("-- Found PCAS:  ${EPICS_PCAS_DIR}")
else()
   message("-- EPICS V3 not included!")
endif()

message("")
message("-- Found ZeroMq: ${ZeroMQ_INCLUDE_DIR}")
message("")
message("-- Found Bzip2: ${BZIP2_INCLUDE_DIR}")
message("----------------------------------------------------------------------")
message("")

